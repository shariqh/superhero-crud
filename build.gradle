plugins {
	id 'org.springframework.boot' version '2.1.4.RELEASE'
	id 'java'
	id "com.palantir.docker" version "0.22.1"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'

group = 'com.shariq.superheromicroservices'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

sourceSets {
	itest
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	itestImplementation.extendsFrom implementation
	itestRuntime.extendsFrom runtime
}

repositories {
	mavenCentral()
}

idea {
	module {
		testSourceDirs += project.sourceSets.itest.java.srcDirs
		testSourceDirs += project.sourceSets.itest.resources.srcDirs

		// put our custom test dependencies onto IDEA's TEST scope
		// Ref: https://docs.gradle.org/current/dsl/org.gradle.plugins.ide.idea.model.IdeaModule.html
		scopes.TEST.plus += [configurations.itestCompileClasspath]
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.data:spring-data-rest-hal-browser'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	itestImplementation 'com.h2database:h2'
	itestImplementation 'org.springframework.boot:spring-boot-starter-test'
	itestImplementation sourceSets.main.output
	itestRuntime configurations.runtime
}

// tag::task[]
bootRun {
	systemProperty 'spring.profiles.active', 'local'
	
	// could also use the following line:
	// args = ["--spring.profiles.active=local"]
}

test {
	// cannot use "args" like in bootRun
	systemProperty 'spring.profiles.active', 'test'
}

task itest(type: Test, group: 'Verification') {
	testClassesDirs = sourceSets.itest.output.classesDirs
	classpath = sourceSets.itest.runtimeClasspath
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/dependency")
}

docker {
	name "shariqhirani/${rootProject.name}"
	copySpec.from(tasks.unpack.outputs).into("dependency")
	buildArgs(['DEPENDENCY': "dependency"])
}
// end::task[]

tasks.check.dependsOn itest
tasks.docker.dependsOn build